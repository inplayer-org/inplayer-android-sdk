apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.github.dcendents.android-maven'
group='com.github.inplayer-org.inplayer-android-sdk'

android {
    compileSdkVersion rootProject.androidCompileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.androidMinSdkVersion
        targetSdkVersion rootProject.androidTargetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASE_URL_PRODUCTION", "\"https://services.inplayer.com/\""

        buildConfigField "String", "BASE_URL_STAGING", "\"https://staging-v2.inplayer.com/\""

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    //Generate documentation ./gradlew dokka
    dokka {
        outputFormat = 'javadoc'
        outputDirectory = "$buildDir/javadoc"

        impliedPlatforms = ["JVM"]

        for (def pkg : ["com.amazonaws",
                        "io.reactivex",
                        "com.sdk.notification",
                        "com.sdk.inplayer.util",
                        "com.sdk.data",
                        "com.sdk.inplayer"]) {
            packageOptions {
                prefix = pkg
                suppress = true
            }
        }

        for (def pkg : ["com.sdk.inplayer.api",
                        "com.sdk.inplayer.model",
                        "com.sdk.inplayer.callback",
                        "com.sdk.inplayer.configuration"]) {
            packageOptions {
                prefix = pkg
                suppress = false
                impliedPlatforms = ["JVM"]
            }
        }


    }
}

dependencies {

    def inPlayerSDK = rootProject.ext.inPlayerSDKDependencies

    def reactive = rootProject.ext.reactive
    def network = rootProject.ext.networkDependencies

    implementation project(':Domain')
    implementation project(':Data')
    implementation project(':Notification')

    implementation network.gson
    implementation network.okHttp
    implementation network.retrofitAdapter

    implementation inPlayerSDK.dependencyInjectionJava
    implementation inPlayerSDK.dependencyInjectionCore

    implementation reactive.rxJava
    implementation reactive.rxAndroid


}
